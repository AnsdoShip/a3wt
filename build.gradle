buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

subprojects {
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    ext {
        libName = 'A3WT'
        libGroupName = 'io.github.a3wtproject'
        libVendorName = 'The A3WT Project'

        libVersionCode = 1
        libVersionName = '0.1.0-SNAPSHOT'

        libSourceCompatibility = JavaVersion.VERSION_1_8
        libTargetCompatibility = JavaVersion.VERSION_1_8

        libAndroidCompileSdk = 33
        libAndroidMinSdk = 16
        libAndroidTargetSdk = 33
    }

    repositories {
        mavenCentral()
        google()
    }

}

configure ([project(':core'), project(':awt')]) {
    apply plugin: 'java'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    sourceCompatibility = libSourceCompatibility
    targetCompatibility = libTargetCompatibility

    group = libGroupName
    version = libVersionName

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = libGroupName
                artifactId = "${rootProject.name}-${project.name}"
                version = libVersionName
                from components.java
                pom {
                    name = libName
                    description = 'Cross-platform direct UI Toolkit for Java. Currently supports Windows/Linux/macOS/Android.'
                    url = 'https://github.com/a3wtproject/a3wt'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'Tianscar'
                            name = 'Tiansword Carbonicacid'
                            email = 'tianscar@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com/a3wtproject/a3wt.git'
                        developerConnection = 'scm:git:git@github.com/a3wtproject/a3wt.git'
                        url = 'https://github.com/a3wtproject/a3wt'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                if (project.version.toString().endsWith("-SNAPSHOT")) {
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                }
                credentials {
                    username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        def signingKey = findProperty("signingKey") ?: System.getenv("GPG_KEY")
        def signingPassword = findProperty("signingPassword") ?:System.getenv("GPG_PASSWORD")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign(publishing.publications.mavenJava)
    }

}